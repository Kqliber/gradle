plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.5.10"
    id "com.gradle.plugin-publish" version "0.14.0"
}

group "me.mattstudios"
version "0.2.0.1"

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.mattstudios.me/artifactory/public" }
    maven { url = "https://jitpack.io" }
}

configurations {
    unzip
    shade
    compileOnly {
        extendsFrom unzip
        extendsFrom shade
    }
}

dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    shade("org.ow2.asm:asm:9.1")
    shade("org.ow2.asm:asm-tree:9.1")
    shade("org.ow2.asm:asm-util:9.1")
    shade("org.ow2.asm:asm-commons:9.1")
    shade "com.google.code.gson:gson:2.8.6"

    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
    testImplementation "io.kotlintest:kotlintest-runner-junit5:3.1.10"

    shade "org.spigotmc:spigot-api:1.16.2-R0.1-SNAPSHOT"

    shade "dev.triumphteam:triumph-gradle-annotations:0.0.1"
}

jar {
    from {
        (configurations.shade + configurations.unzip)
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

gradlePlugin {
    plugins {
        triumph {
            id = "me.mattstudios.triumph"
            displayName = "Triumph Gradle"
            description = "Plugin with utilities for the Triumph projects."
            implementationClass = "dev.triumphteam.TriumphPlugin"
        }
    }
}

pluginBundle {
    website = "https://mf.mattstudios.me"
    vcsUrl = "https://github.com/TriumphDev/gradle"
    tags = ["spigot", "bukkit", "minecraft"]
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}